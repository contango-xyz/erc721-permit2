//SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

// inspired by
//  - https://github.com/Uniswap/permit2/blob/main/src/Permit2.sol
//  - https://github.com/Uniswap/permit2/blob/main/src/SignatureTransfer.sol

import "@openzeppelin/contracts/token/ERC721/IERC721.sol";
import "@openzeppelin/contracts/utils/cryptography/EIP712.sol";

import "./interfaces/IERC721Permit2.sol";
import "./libraries/PermitHash.sol";
import "./libraries/SignatureVerification.sol";

contract ERC721Permit2 is IERC721Permit2, EIP712("ERC721Permit2", "1") {
    using SignatureVerification for bytes;
    using PermitHash for *;

    /// @inheritdoc IERC721Permit2
    mapping(address owner => mapping(uint256 wordPos => uint256 bitMap)) public nonceBitmap;

    /// @inheritdoc IERC721Permit2
    // solhint-disable-next-line func-name-mixedcase
    function DOMAIN_SEPARATOR() external view override returns (bytes32) {
        return _domainSeparatorV4();
    }

    /// @inheritdoc IERC721Permit2
    function permitTransferFrom(
        PermitTransferFrom memory permit,
        SignatureTransferDetails calldata transferDetails,
        address owner,
        bytes calldata signature
    ) external override {
        _permitTransferFrom(permit, transferDetails, owner, permit.hash(), signature, false);
    }

    /// @inheritdoc IERC721Permit2
    function permitSafeTransferFrom(
        PermitTransferFrom memory permit,
        SignatureTransferDetails calldata transferDetails,
        address owner,
        bytes calldata signature
    ) external override {
        _permitTransferFrom(permit, transferDetails, owner, permit.hash(), signature, true);
    }

    /// @notice Transfers a token using a signed permit message.
    /// @param permit The permit data signed over by the owner
    /// @param dataHash The EIP-712 hash of permit data to include when checking signature
    /// @param owner The owner of the tokens to transfer
    /// @param transferDetails The spender's requested transfer details for the permitted token
    /// @param signature The signature to verify
    function _permitTransferFrom(
        PermitTransferFrom memory permit,
        SignatureTransferDetails calldata transferDetails,
        address owner,
        bytes32 dataHash,
        bytes calldata signature,
        bool safeTransfer
    ) private {
        uint256 tokenId = transferDetails.tokenId;

        if (block.timestamp > permit.deadline) revert SignatureExpired(permit.deadline);
        if (tokenId != permit.permitted.tokenId) revert InvalidTokenId(permit.permitted.tokenId);

        _useUnorderedNonce(owner, permit.nonce);

        signature.verify(_hashTypedDataV4(dataHash), owner);

        _transfer(permit.permitted.token, owner, transferDetails.to, tokenId, safeTransfer);
    }

    /// @inheritdoc IERC721Permit2
    function permitTransferFrom(
        PermitBatchTransferFrom memory permit,
        SignatureTransferDetails[] calldata transferDetails,
        address owner,
        bytes calldata signature
    ) external override {
        _permitTransferFrom(permit, transferDetails, owner, permit.hash(), signature, false);
    }

    /// @inheritdoc IERC721Permit2
    function permitSafeTransferFrom(
        PermitBatchTransferFrom memory permit,
        SignatureTransferDetails[] calldata transferDetails,
        address owner,
        bytes calldata signature
    ) external override {
        _permitTransferFrom(permit, transferDetails, owner, permit.hash(), signature, true);
    }

    /// @notice Transfers tokens using a signed permit messages
    /// @param permit The permit data signed over by the owner
    /// @param dataHash The EIP-712 hash of permit data to include when checking signature
    /// @param owner The owner of the tokens to transfer
    /// @param signature The signature to verify
    function _permitTransferFrom(
        PermitBatchTransferFrom memory permit,
        SignatureTransferDetails[] calldata transferDetails,
        address owner,
        bytes32 dataHash,
        bytes calldata signature,
        bool safeTransfer
    ) private {
        uint256 numPermitted = permit.permitted.length;

        if (block.timestamp > permit.deadline) revert SignatureExpired(permit.deadline);
        if (numPermitted != transferDetails.length) revert LengthMismatch();

        _useUnorderedNonce(owner, permit.nonce);
        signature.verify(_hashTypedDataV4(dataHash), owner);

        unchecked {
            for (uint256 i = 0; i < numPermitted; ++i) {
                TokenPermissions memory permitted = permit.permitted[i];
                uint256 tokenId = transferDetails[i].tokenId;

                if (tokenId != permitted.tokenId) revert InvalidTokenId(permitted.tokenId);

                _transfer(permitted.token, owner, transferDetails[i].to, tokenId, safeTransfer);
            }
        }
    }

    function _transfer(address token, address from, address to, uint256 tokenId, bool safeTransfer) internal {
        if (safeTransfer) IERC721(token).safeTransferFrom(from, to, tokenId);
        else IERC721(token).transferFrom(from, to, tokenId);
    }

    /// @inheritdoc IERC721Permit2
    function invalidateUnorderedNonces(uint256 wordPos, uint256 mask) external override {
        nonceBitmap[msg.sender][wordPos] |= mask;

        emit UnorderedNonceInvalidation(msg.sender, wordPos, mask);
    }

    /// @notice Returns the index of the bitmap and the bit position within the bitmap. Used for unordered nonces
    /// @param nonce The nonce to get the associated word and bit positions
    /// @return wordPos The word position or index into the nonceBitmap
    /// @return bitPos The bit position
    /// @dev The first 248 bits of the nonce value is the index of the desired bitmap
    /// @dev The last 8 bits of the nonce value is the position of the bit in the bitmap
    function bitmapPositions(uint256 nonce) private pure returns (uint256 wordPos, uint256 bitPos) {
        wordPos = uint248(nonce >> 8);
        bitPos = uint8(nonce);
    }

    /// @notice Checks whether a nonce is taken and sets the bit at the bit position in the bitmap at the word position
    /// @param from The address to use the nonce at
    /// @param nonce The nonce to spend
    function _useUnorderedNonce(address from, uint256 nonce) internal {
        (uint256 wordPos, uint256 bitPos) = bitmapPositions(nonce);
        uint256 bit = 1 << bitPos;
        uint256 flipped = nonceBitmap[from][wordPos] ^= bit;

        if (flipped & bit == 0) revert InvalidNonce();
    }
}
